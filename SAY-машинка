// Скетч написан Александром Слука.
// У кого есть два двигателя, Ардуино Уно, драйверы L258N, ИК-пульт, 
// ИК-приемник и аккумулятор 18650(4шт) или батарейки АА(4шт) - 
// можите собрать машинку своему ребенку  

#define PIN_RECV  13 // Вывод, к которому подключен ИК-приёмник
#define PIN_IN1   7   // Вывод управления рулевого движка
#define PIN_IN2   6   // Вывод управления рулевого движка
#define PIN_IN3   5   // Вывод управления моторного движка
#define PIN_IN4   8   // Вывод управления моторного движка
#define PIN_ENA   9   // Вывод управления скоростью рулевого движка
#define PIN_ENB   4   // Вывод управления скоростью моторного движка
#define SPEED 255     // Скорость SAY-машинки (0-255)

// Коды с пульта управления (необходимо вставить свои КОДЫ!!!)
#define BUTTON_FORWARD  0x926DD02F // Код кнопки ВПЕРЁД
#define BUTTON_LEFT  0x926DF20D // Код кнопки ВЛЕВО
#define BUTTON_RIGHT  0x926D906F // Код кнопки ВПРАВО
#define BUTTON_BACK  0x926D32CD // Код кнопки НАЗАД
#define BUTTON_STOP  0x926DB24D // Код кнопки СТОП

#include <IRremote.h> // Библиотека для работы с ИК-приёмником
IRrecv irrecv(PIN_RECV); // Создание объекта работы с ИК-приёмником
decode_results results; // Переменная для хранения результата декодирования

void setup() {
  irrecv.enableIRIn();  // Инициализация ИК-приёмника
  // Настройка на выход всех управляющих пинов Arduino Uno
  pinMode(PIN_IN1, OUTPUT);
  pinMode(PIN_IN2, OUTPUT);
  pinMode(PIN_IN3, OUTPUT);
  pinMode(PIN_IN4, OUTPUT);
  pinMode(PIN_ENA, OUTPUT);
  pinMode(PIN_ENB, OUTPUT);
  // Остановка моторов
  digitalWrite(PIN_IN1, LOW);
  digitalWrite(PIN_IN2, LOW);
  digitalWrite(PIN_IN3, LOW);
  digitalWrite(PIN_IN4, LOW);
  analogWrite(PIN_ENA, SPEED);
  analogWrite(PIN_ENB, SPEED);
}
void loop() {
  // Ждём поступления сигнала с пульта ДУ
  if (irrecv.decode(&results)) {
    // Анализируем полученный результат
    switch(results.value) {
        case BUTTON_FORWARD: // Движение ВПЕРЁД
          digitalWrite(PIN_IN3, HIGH);
          digitalWrite(PIN_IN4, LOW);
        break;
        case BUTTON_LEFT: // Поворот ВЛЕВО
          digitalWrite(PIN_IN1, HIGH);
          digitalWrite(PIN_IN2, LOW);
        break;
        case BUTTON_RIGHT: // Поворот ВПРАВО
          digitalWrite(PIN_IN1, LOW);
          digitalWrite(PIN_IN2, HIGH);
        break;
        case BUTTON_BACK: // Движение НАЗАД
          digitalWrite(PIN_IN3, LOW);
          digitalWrite(PIN_IN4, HIGH);
        break;
       case BUTTON_STOP: // Движение CТОП
          digitalWrite(PIN_IN3, LOW);
          digitalWrite(PIN_IN4, LOW);
          digitalWrite(PIN_IN1, LOW);
          digitalWrite(PIN_IN2, LOW);
        break;  
    }
   
    irrecv.resume();
  }
}
